mod concave_component
mod convex_component
mod convexity
mod line_segment

pub use concave_component::*
pub use convex_component::*
pub use line_segment::*

use crate::geom2d::*
use crate::raw_contour::RawContour
use line_segment::*

pub struct LineSegmentStroke {
    points: ~CyclicSlice Point2d,
    start: Point2d := self.points.first()!.clone(),
    end: Point2d := self.points.last()!.clone(),
}

impl Visualize for LineSegmentStroke:
    fn visualize():
        <LineSegment start = {self.start} end = {self.end} />

impl LineSegmentStroke:
    static fn new(ct: ~RawContour, from: i32, to: i32) -> LineSegmentStroke:
        assert from <= to
        LineSegmentStroke(ct.points.cyclic_slice_leashed(from, to + 1))

    fn displacement() -> Vector2d:
        self.start.to(self.end)

pub struct LineSegmentSketch {
    contour: ~RawContour,
    strokes: []LineSegmentStroke,
}

impl Visualize for LineSegmentSketch:
    fn visualize() -> Html:
        self.strokes.visualize()

impl LineSegmentSketch:
    val concave_components: []ConcaveComponent =
        find_concave_components(self)
    
    val bounding_box: BoundingBox =
        let start_point = self.strokes[0].start
        let mut xmin = start_point.x
        let mut xmax = start_point.x
        let mut ymin = start_point.y
        let mut ymax = start_point.y
        for i < self.strokes.ilen():
            let point = self.strokes[i].end
            xmin = xmin.min(point.x)
            xmax = xmax.max(point.x)
            ymin = ymin.min(point.y)
            ymax = ymax.max(point.y)
        return BoundingBox(
            ClosedRange(xmin, xmax), 
            ClosedRange(ymin, ymax),
        )

    static fn new(ct: ~RawContour, r: f32) -> LineSegmentSketch:
        LineSegmentSketch(ct, find_line_segments(ct, r))

fn go_right(u: Vector2d, r: f32) -> Vector2d:
    let L = (u.x*u.x+u.y*u.y).sqrt()
    assert L > r
    let dr = r*L/(L*L-r*r).sqrt()
    let dx = dr*u.y/L
    let dy = -dr*u.x/L
    Vector2d(u.x+dx, u.y+dy)

fn go_left(u: Vector2d, r: f32) -> Vector2d:
    let L = (u.x*u.x+u.y*u.y).sqrt()
    assert L > r
    let dr = r*L/(L*L-r*r).sqrt()
    let dx = -dr*u.y/L
    let dy = dr*u.x/L
    Vector2d(u.x+dx, u.y+dy)

fn extend_end(ct: ~RawContour, start: i32, r: f32) -> i32:
    let mut end = start
    let mut dp = ct.displacement(start, end + 1)
    let N = ct.points.ilen()
    let max_end = start + N
    while end <= max_end and dp.norm() < r:
        end++
        dp = ct.displacement(start, end + 1)
    if dp.norm() < r:
        return end
    let mut right_bound = go_right(dp, r)
    let mut left_bound = go_left(dp, r)
    let mut r_max = 0.0
    while end <= max_end
            and right_bound.rotation_direction_to(dp) >= 0 
            and dp.rotation_direction_to(left_bound) >= 0:
        let dp_norm = dp.norm()
        if dp_norm < r_max - r:
            break
        elif dp_norm > r_max:
            r_max = dp_norm
        if dp_norm > r:
            let dp_right = go_right(dp, r)
            let dp_left = go_left(dp, r)
            if right_bound.rotation_direction_to(dp_right) > 0:
                right_bound = dp_right
            if dp_left.rotation_direction_to(left_bound) > 0:
                left_bound = dp_left
        end++
        dp = ct.displacement(start, end + 1)
    assert end > start
    return end

fn extend_start(ct: ~RawContour, start0: i32, end: i32, r: f32) -> i32:
    let mut start = end
    let mut dp0 = ct.displacement(end, start - 1)
    let min_start = end - ct.points.ilen()
    while start >= min_start and dp0.norm() < r:
        start--
        dp0 = ct.displacement(end, start - 1)
    if dp0.norm() < r:
        return start.min(start0)
    let mut right_bound = go_right(dp0, r)
    let mut left_bound = go_left(dp0, r)
    let mut r_max = 0.0
    while start >= min_start:
        let dp = ct.displacement(end, start - 1)
        let dp_norm = dp.norm()
        if dp_norm < r_max - r:
            break
        elif dp_norm > r_max:
            r_max = dp_norm
        if dp_norm > r:
            let dp_right = go_right(dp, r)
            let dp_left = go_left(dp, r)
            if right_bound.rotation_direction_to(dp_right) > 0:
                right_bound = dp_right
            if dp_left.rotation_direction_to(left_bound) > 0:
                left_bound = dp_left
        if right_bound.rotation_direction_to(left_bound) >= 0:
            if start <= start0 
                    and !(
                        right_bound.rotation_direction_to(dp) >= 0 
                        and dp.rotation_direction_to(left_bound) >= 0
                    ):
                break
            start--
        else:
            break
    //assert start <= start0
    if start <= start0:
        return start
    else:
        return start0

fn find_line_segments(ct: ~RawContour, r: f32) -> []LineSegmentStroke:
    let mut line_segments: []LineSegmentStroke = []
    let mut start = 0
    let mut end = 1
    let mut max_end = ct.points.ilen()
    while end <= max_end:
        end = extend_end(ct, start, r)
        let ls_extend_end = LineSegmentStroke::new(ct, start, end)
        let mut extend_start_flag = true
        if line_segments.ilen() > 0:
            let dp_extend_end = ls_extend_end.displacement()
            let dp_previous = line_segments.last()!.displacement()
            if dp_extend_end.cross(dp_previous).abs() < 0.01 
                    and dp_extend_end.dot(dp_previous) > 0.0:
                let N = ct.points.ilen()
                line_segments.last()! = LineSegmentStroke::new(ct, line_segments.last()!.points.start(), end)
                extend_start_flag = false
        if extend_start_flag:
            start = extend_start(ct, start, end, r)
            let mut ls = LineSegmentStroke::new(ct, start, end)
            if line_segments.ilen() > 0:
                let ls_last = line_segments.last()!
                let dp_last = ls_last.displacement()
                let dp = ls.displacement()
                let dp1 = ls_last.start.to(ls.end)
                if dp.cross(dp_last).abs() < 0.001 
                        and dp.dot(dp_last) > 0.0 
                        and dp.cross(dp1).abs()<0.001 
                        and dp.dot(dp1) > 0.0:
                    let ls_last = line_segments.pop()!
                    ls = LineSegmentStroke::new(ct, ls_last.points.start(), ls.points.end())
            else:
                max_end = start + ct.points.ilen()
            line_segments.push(ls)
        start = end
        end = start + 1
    let N = ct.points.ilen()
    let first_line_segment_points_end = line_segments.first()!.points.end()
    let last_line_segment = line_segments.last()!
    if last_line_segment.points.end() >= first_line_segment_points_end + N:
        let last_line_segment = line_segments.pop()!
        line_segments.first()! = LineSegmentStroke::new(
            ct,
            last_line_segment.points.start() - N,
            line_segments.first()!.points.end() - 1
        )
    line_segments